plugins {
    // See:
    //      https://github.com/ajoberstar/gradle-git/
    id 'org.ajoberstar.github-pages' version '1.7.2' // Not support '+' symbol!
}

apply plugin: 'java'

tasks.withType(JavaCompile) {
    // See: 
    //      https://stackoverflow.com/questions/21028438/
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    // See: 
    //      https://mrhaki.blogspot.ru/2012/06/gradle-goodness-set-java-compiler.html
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    // See:
    //      http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
    options.addStringOption('Xdoclint:none', '-quiet')
}

test {
     maxHeapSize = '512m' // To be compatible with Travis-CI infrastructure.
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        name    'JitPack'
        url     'https://jitpack.io/' 
    }
    maven {
        name    'Actual Xuggler repo'
        url     'https://raw.github.com/olivierayache/xuggle-xuggler/repos/'
    }
}

dependencies {
    compile     'gov.nist.math:jama:1.+'
    compile     'com.github.kerner1000:javapng:2.+'
    compile     'org.slf4j:slf4j-api:1.+'
    compile     'org.slf4j:slf4j-simple:1.+'
    compile     'xuggle:xuggle-xuggler:5.5' // Not support '+' symbol!
    compile     'com.github.dzavodnikov:JParFor:1.+'

    testCompile 'junit:junit:4.+'
}

// See:
//      https://github.com/ajoberstar/gradle-git/wiki/org.ajoberstar.github-pages/
githubPages {
    pages {
        from javadoc
    }
}

apply plugin: 'eclipse'

// See: 
//      https://stackoverflow.com/questions/10156847/
eclipse.classpath {
    downloadSources = true
    downloadJavadoc = true
}

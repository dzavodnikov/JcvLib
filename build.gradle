plugins {
    // See:
    //      https://github.com/ajoberstar/gradle-git-publish/
    id 'org.ajoberstar.git-publish' version '0.3.2'
}

apply plugin: 'java'

tasks.withType(JavaCompile) {
    // See: 
    //      https://stackoverflow.com/questions/21028438/
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    // See: 
    //      https://mrhaki.blogspot.ru/2012/06/gradle-goodness-set-java-compiler.html
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    // See:
    //      http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
    options.addStringOption('Xdoclint:none', '-quiet')
}

test {
    maxHeapSize = '512m' // To be compatible with Travis-CI infrastructure.
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        name    'JitPack'
        url     'https://jitpack.io/' 
    }
}

dependencies {
    // https://mvnrepository.com/artifact/gov.nist.math/jama
    compile group: 'gov.nist.math', name: 'jama', version: '1.0.3'

    // https://jitpack.io/#depsypher/pngtastic/pngtastic-1.5
    compile 'com.github.depsypher:pngtastic:pngtastic-1.5'

    // https://jitpack.io/#dzavodnikov/JParFor/1.1.29
    compile 'com.github.dzavodnikov:JParFor:1.1.29'

    // https://mvnrepository.com/artifact/junit/junit
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// See:
//      https://github.com/ajoberstar/gradle-git-publish/
gitPublish {
    // Where to publish to (repo must exist).
    repoUri = 'git@github.com/dzavodnikov/JcvLib.git'
    // Branch will be created if it doesn't exist.
    branch = 'gh-pages'

    // What to publish, this is a standard CopySpec.
    contents {
        from 'src/pages'
        from(javadoc) {
            into 'api'
        }
    }
}

apply plugin: 'eclipse'

// See: 
//      https://stackoverflow.com/questions/10156847/
eclipse.classpath {
    downloadSources = true
    downloadJavadoc = true
}
